#!/bin/zsh

TAGDIR=$HOME/.plaintag
mkdir -p $TAGDIR


help()
{
    <<EOF
Usage:
	$0 list     <tag>  [<tags>...] -- list files tagged with all of these tags
	$0 listany  <tag>  [<tags>...] -- list files tagged with any of these tags
	$0 show     <file>             -- list tags associated with a file
	$0 [+|-]tag <file> [<tags>...] -- add, remove or overwrite a tag list associated with a file
EOF
}


# disassociate a tag with a file
add_tags()
{
    local file tag

    file=$1
    if ! shift 2> /dev/null; then
        help
        return 1
    fi

    for tag in $TAGDIR/$^argv; do
        touch $tag
        echo $file:a | sort --output=$tag --merge --unique -- - $tag
    done
}

# associate a new tag with a file
rm_tags()
{
    local file

    file=$1
    if ! shift 2> /dev/null; then
        help
        return 1
    fi

    if [[ -n $argv ]]; then
        perl -ni -e '
BEGIN {
    $file = shift;
    @tags = @ARGV;
}
print if not /^\Q$file\E$/;
END {
    unlink for grep {-z $_} @tags
}' $file:a $TAGDIR/$^argv 2> /dev/null
    fi
}

# show tags associated with a file
show_tags()
{
    local file all_tags

    file=$1:a
    if ! shift 2> /dev/null; then
        help
        return 1
    fi

    pushd -q $TAGDIR
    all_tags=(*(N))
    if [[ -n $all_tags ]]; then
        grep --fixed-strings --line-regexp --files-with-matches -- $file $all_tags 2> /dev/null
    fi
    popd -q
}

# list files with any of the given tags
list_tagged_any()
{
    local tags

    pushd -q $TAGDIR
    tags=(*(N))
    tags=(${tags:*argv})        # get only the existing tags
    if [[ -n $tags ]]; then
        < $tags | sort --unique
    fi
    popd -q
}

# list files with all of the given tags
list_tagged()
{
    local tag tags files tmp

    pushd -q $TAGDIR
    all_tags=(*(N))
    # do nothing if any of these tags does not exist or if the list is empty
    if [[ -n ${argv:|all_tags} || -z $argv ]]; then
        return
    fi
    tag=$1
    shift
    IFS=$'\n'
    files=($(< $tag))
    for tag in $argv; do
        tmp=($(< $tag))
        files=${files:*tmp}
    done
    unset IFS
    popd -q
    print -l $files
}

# overwrite the file's current tags with a new set of tags
set_tags()
{
    local file new_tags current_tags tags_to_add tags_to_remove

    file=$1
    if ! shift 2> /dev/null; then
        help
        return 1
    fi

    IFS=$'\n'
    new_tags=($argv)
    current_tags=($(show_tags $file))
    tags_to_add=(${new_tags:|current_tags})
    tags_to_remove=(${current_tags:|new_tags})
    unset IFS

    add_tags $file $tags_to_add
    rm_tags $file $tags_to_remove
}


cmd=$1
if ! shift 2> /dev/null; then
    help
    return 1
fi

case $cmd in
    --help|-h|help)
        help ;;
    list)
        list_tagged $argv ;;
    listany)
        list_tagged_any $argv ;;
    show)
        show_tags $argv ;;
    +tag)
        add_tags $argv ;;
    -tag)
        rm_tags $argv ;;
    tag)
        set_tags $argv ;;
    *)
        help; return 1 ;;
esac
